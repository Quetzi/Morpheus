buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

project.ext {
    major = "3"
    minor = "3"
    if (System.getenv("BUILD_NUMBER") != null) {
        build = System.getenv("BUILD_NUMBER")
    } else {
        build = "-custom"
    }
    mcVersion = "1.12"
}
version = "${project.ext.mcVersion}-${project.ext.major}.${project.minor}.${project.build}"
group = "net.quetzi.morpheus"
archivesBaseName = "Morpheus"

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "${project.ext.mcVersion}-14.21.0.2321"
    runDir = "run"
    mappings = "snapshot_20170610"

    // replacing stuff in the source
    replace '@MAJOR@', project.major
    replace '@MINOR@', project.minor
    replace '@MC_VERSION@', project.ext.mcVersion
    replace '@BUILD_NUMBER@', project.build
}

task libJar(type: Jar) {
    from(sourceSets.main.java) {
        include "net/quetzi/morpheus/api/*"
    }
    classifier = 'api'
}

artifacts {
    archives libJar
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
